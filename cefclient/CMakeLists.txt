# Copyright (c) 2014 The Chromium Embedded Framework Authors. All rights
# reserved. Use of this source code is governed by a BSD-style license that
# can be found in the LICENSE file.

#
# Source files.
#

# cefclient sources.
set(CEFCLIENT_SRCS
  binding_test.cc
  binding_test.h
  bytes_write_handler.cc
  bytes_write_handler.h
  client_app.cc
  client_app.h
  client_app_delegates.cc
  client_handler.cc
  client_handler.h
  client_renderer.cc
  client_renderer.h
  client_switches.cc
  client_switches.h
  dialog_test.cc
  dialog_test.h
  main_context.cc
  main_context.h
  main_context_impl.cc
  main_context_impl.h
  main_message_loop.cc
  main_message_loop.h
  main_message_loop_std.cc
  main_message_loop_std.h
  osr_dragdrop_events.h
  osr_renderer.cc
  osr_renderer.h
  performance_test.cc
  performance_test.h
  performance_test_setup.h
  performance_test_tests.cc
  resource.h
  resource_util.h
  scheme_test.cc
  scheme_test.h
  test_runner.cc
  test_runner.h
  window_test.cc
  window_test.h
  )
set(CEFCLIENT_SRCS_LINUX
  cefclient_gtk.cc
  client_handler_gtk.cc
  dialog_handler_gtk.cc
  dialog_handler_gtk.h
  main_context_impl_posix.cc
  osr_widget_gtk.cc
  osr_widget_gtk.h
  print_handler_gtk.cc
  print_handler_gtk.h
  resource_util_linux.cc
  resource_util_posix.cc
  window_test_gtk.cc
  )
set(CEFCLIENT_SRCS_MACOSX
  cefclient_mac.mm
  client_handler_mac.mm
  main_context_impl_posix.cc
  osr_widget_mac.h
  osr_widget_mac.mm
  resource_util_mac.mm
  resource_util_posix.cc
  window_test_mac.mm
  )
set(CEFCLIENT_SRCS_WINDOWS
  cefclient.exe.manifest
  cefclient.rc
  cefclient_win.cc
  client_handler_win.cc
  main_context_impl_win.cc
  main_message_loop_multithreaded_win.cc
  main_message_loop_multithreaded_win.h
  osr_dragdrop_win.cc
  osr_dragdrop_win.h
  osr_widget_win.cc
  osr_widget_win.h
  resource_util_win.cc
  util_win.cc
  util_win.h
  window_test_win.cc
  )
APPEND_PLATFORM_SOURCES(CEFCLIENT_SRCS)
source_group(cefclient FILES ${CEFCLIENT_SRCS})

set(CEFCLIENT_RES_SRCS
  res/binding.html
  res/dialogs.html
  res/localstorage.html
  res/logo.png
  res/osr_test.html
  res/other_tests.html
  res/performance.html
  res/performance2.html
  res/transparency.html
  res/window.html
  res/xmlhttprequest.html
  )
set(CEFCLIENT_RES_SRCS_WINDOWS
  res/cefclient.ico
  res/small.ico
  )
APPEND_PLATFORM_SOURCES(CEFCLIENT_RES_SRCS)
source_group(cefclient\\\\res FILES ${CEFCLIENT_RES_SRCS})

set(CEFCLIENT_SRCS
  ${CEFCLIENT_SRCS}
  ${CEFCLIENT_RES_SRCS}
  )

# cefclient helper sources.
set(CEFCLIENT_HELPER_SRCS_MACOSX
  binding_test.cc
  binding_test.h
  client_app.cc
  client_app.h
  client_app_delegates.cc
  client_handler.cc
  client_handler.h
  client_handler_mac.mm
  client_renderer.cc
  client_renderer.h
  client_switches.cc
  client_switches.h
  dialog_test.cc
  dialog_test.h
  main_context.cc
  main_context.h
  main_message_loop.cc
  main_message_loop.h
  performance_test.cc
  performance_test.h
  performance_test_setup.h
  performance_test_tests.cc
  process_helper_mac.cc
  resource.h
  resource_util.h
  resource_util_mac.mm
  resource_util_posix.cc
  scheme_test.cc
  scheme_test.h
  test_runner.cc
  test_runner.h
  window_test.cc
  window_test.h
  window_test_mac.mm
  )
APPEND_PLATFORM_SOURCES(CEFCLIENT_HELPER_SRCS)
source_group(cefclient FILES ${CEFCLIENT_HELPER_SRCS})

# cefclient resources.
set(CEFCLIENT_RESOURCES_MAC_SRCS_MACOSX
  mac/Info.plist
  mac/cefclient.icns
  )
APPEND_PLATFORM_SOURCES(CEFCLIENT_RESOURCES_MAC_SRCS)
source_group(cefclient\\\\mac FILES ${CEFCLIENT_RESOURCES_MAC_SRCS})

set(CEFCLIENT_RESOURCES_MAC_ENGLISH_LPROJ_SRCS_MACOSX
  mac/English.lproj/InfoPlist.strings
  mac/English.lproj/MainMenu.xib
  )
APPEND_PLATFORM_SOURCES(CEFCLIENT_RESOURCES_MAC_ENGLISH_LPROJ_SRCS)
source_group(cefclient\\\\mac\\\\English.lproj FILES ${CEFCLIENT_RESOURCES_MAC_ENGLISH_LPROJ_SRCS})

set(CEFCLIENT_RESOURCES_RES_SRCS
  res/binding.html
  res/dialogs.html
  res/localstorage.html
  res/logo.png
  res/osr_test.html
  res/other_tests.html
  res/performance.html
  res/performance2.html
  res/transparency.html
  res/window.html
  res/xmlhttprequest.html
  )
source_group(cefclient\\\\res FILES ${CEFCLIENT_RESOURCES_RES_SRCS})

set(CEFCLIENT_RESOURCES_SRCS
  ${CEFCLIENT_RESOURCES_MAC_SRCS}
  ${CEFCLIENT_RESOURCES_MAC_ENGLISH_LPROJ_SRCS}
  ${CEFCLIENT_RESOURCES_RES_SRCS}
  )


#
# Shared configuration.
#

# Target executable names.
set(CEF_TARGET "cefclient")
if(OS_MACOSX)
  set(CEF_HELPER_TARGET "cefclient Helper")
endif()

# Logical target used to link the libcef library.
ADD_LOGICAL_TARGET("libcef_lib" "${CEF_LIB_DEBUG}" "${CEF_LIB_RELEASE}")

# Logical target used to link the libcef wrapper library.
ADD_LOGICAL_TARGET("libcef_dll_wrapper" "${CEF_LIB_WRAPPER_DEBUG}" "${CEF_LIB_WRAPPER_RELEASE}")

# Determine the target output directory.
SET_CEF_TARGET_OUT_DIR()


#
# Linux configuration.
#

if(OS_LINUX)
  # Find required libraries and update compiler/linker variables.
  FIND_LINUX_LIBRARIES("gmodule-2.0 gtk+-2.0 gthread-2.0 gtk+-unix-print-2.0 gtkglext-1.0")

  # Executable target.
  add_executable(${CEF_TARGET} ${CEFCLIENT_SRCS})
  add_dependencies(${CEF_TARGET} libcef_dll_wrapper)
  target_link_libraries(${CEF_TARGET} libcef_lib libcef_dll_wrapper ${CEF_STANDARD_LIBS})

  # Set rpath so that libraries can be placed next to the executable.
  set_target_properties(${CEF_TARGET} PROPERTIES INSTALL_RPATH "$ORIGIN")
  set_target_properties(${CEF_TARGET} PROPERTIES BUILD_WITH_INSTALL_RPATH TRUE)
  set_target_properties(${CEF_TARGET} PROPERTIES RUNTIME_OUTPUT_DIRECTORY ${CEF_TARGET_OUT_DIR})

  # Copy CEF binary and resource files to the target output directory.
  COPY_FILES("${CEF_TARGET}" "${CEF_BINARY_FILES}" "${CEF_BINARY_DIR}" "${CEF_TARGET_OUT_DIR}")
  COPY_FILES("${CEF_TARGET}" "${CEF_RESOURCE_FILES}" "${CEF_RESOURCE_DIR}" "${CEF_TARGET_OUT_DIR}")

  # Copy cefclient resource files to the target output directory.
  COPY_FILES("${CEF_TARGET}" "${CEFCLIENT_RESOURCES_SRCS}" "${CMAKE_CURRENT_SOURCE_DIR}" "${CEF_TARGET_OUT_DIR}")
  # Rename the "res" directory to "files".
  RENAME_DIRECTORY("${CEF_TARGET}" "${CEF_TARGET_OUT_DIR}/res" "${CEF_TARGET_OUT_DIR}/files")

  # Set SUID permissions on the chrome-sandbox target.
  SET_LINUX_SUID_PERMISSIONS("${CEF_TARGET}" "${CEF_TARGET_OUT_DIR}/chrome-sandbox")
endif()


#
# Mac OS X configuration.
#

if(OS_MACOSX)
  # Output paths for the app bundles.
  set(CEF_APP "${CEF_TARGET_OUT_DIR}/${CEF_TARGET}.app")
  set(CEF_HELPER_APP "${CEF_TARGET_OUT_DIR}/${CEF_HELPER_TARGET}.app")

  # Variable referenced from Info.plist files.
  set(PRODUCT_NAME "${CEF_TARGET}")


  # Helper executable target.
  add_executable(${CEF_HELPER_TARGET} MACOSX_BUNDLE ${CEFCLIENT_HELPER_SRCS})
  add_dependencies(${CEF_HELPER_TARGET} cef)
  target_link_libraries(${CEF_HELPER_TARGET} libcef_lib libcef_dll_wrapper ${CEF_STANDARD_LIBS})
  set_target_properties(${CEF_HELPER_TARGET} PROPERTIES
    MACOSX_BUNDLE_INFO_PLIST ${CMAKE_CURRENT_SOURCE_DIR}/mac/helper-Info.plist
    )

  # Fix the framework link in the helper executable.
  FIX_MACOSX_HELPER_FRAMEWORK_LINK(${CEF_HELPER_TARGET} ${CEF_HELPER_APP})


  # Logical target used to link the syphon library.
  ADD_LOGICAL_TARGET("syphon_lib" "${SYPHON_LIB_DEBUG}" "${SYPHON_LIB_RELEASE}")

  # Main executable target.
  add_executable(${CEF_TARGET} MACOSX_BUNDLE ${CEFCLIENT_RESOURCES_SRCS} ${CEFCLIENT_SRCS})
  add_dependencies(${CEF_TARGET} ${CEF_HELPER_TARGET} cef syphon)
  target_link_libraries(${CEF_TARGET} libcef_lib libcef_dll_wrapper syphon_lib  ${CEF_STANDARD_LIBS} "-framework OpenGL")
  set_target_properties(${CEF_TARGET} PROPERTIES
    RESOURCE "${CEFCLIENT_RESOURCES_SRCS}"
    MACOSX_BUNDLE_INFO_PLIST ${CMAKE_CURRENT_SOURCE_DIR}/mac/Info.plist
    )

  # Copy files into the main app bundle.
  add_custom_command(
    TARGET ${CEF_TARGET}
    POST_BUILD
    # Copy the helper app bundle into the Frameworks directory.
    COMMAND ${CMAKE_COMMAND} -E copy_directory
            "${CEF_HELPER_APP}"
            "${CEF_APP}/Contents/Frameworks/${CEF_HELPER_TARGET}.app"
    # Copy the CEF framework into the Frameworks directory.
    COMMAND ${CMAKE_COMMAND} -E copy_directory
            "${CEF_BINARY_DIR}/Chromium Embedded Framework.framework"
            "${CEF_APP}/Contents/Frameworks/Chromium Embedded Framework.framework"
    # Copy the Syphon framework into the Frameworks directory.
    COMMAND ${CMAKE_COMMAND} -E copy_directory
            "${SYPHON_BINARY_DIR}/Syphon.framework"
            "${CEF_APP}/Contents/Frameworks/Syphon.framework"
    VERBATIM
    )

  # Fix the framework link in the main executable.
  FIX_MACOSX_MAIN_FRAMEWORK_LINK(${CEF_TARGET} ${CEF_APP})

  # Make the other helper app bundles.
  MAKE_MACOSX_HELPERS(${CEF_TARGET} ${CEF_APP})

  if(NOT ${CMAKE_GENERATOR} STREQUAL "Xcode")
    # Manually process and copy over resource files.
    # The Xcode generator handles this via the set_target_properties RESOURCE directive.
    set(PREFIXES "mac/" "res/")  # Remove these prefixes from input file paths.
    COPY_MACOSX_RESOURCES("${CEFCLIENT_RESOURCES_SRCS}" "${PREFIXES}" "${CEF_TARGET}" "${CMAKE_CURRENT_SOURCE_DIR}" "${CEF_APP}")
  endif()
endif()


#
# Windows configuration.
#

if(OS_WINDOWS)
  # Executable target.
  add_executable(${CEF_TARGET} WIN32 ${CEFCLIENT_SRCS})
  target_link_libraries(${CEF_TARGET} libcef_lib libcef_dll_wrapper ${CEF_STANDARD_LIBS} "glu32.lib" "opengl32.lib")

  if(USE_SANDBOX)
    # Logical target used to link the cef_sandbox library.
    ADD_LOGICAL_TARGET("cef_sandbox_lib" "${CEF_SANDBOX_LIB_DEBUG}" "${CEF_SANDBOX_LIB_RELEASE}")
    target_link_libraries(${CEF_TARGET} cef_sandbox_lib ${CEF_SANDBOX_STANDARD_LIBS})
  endif()

  # Add the custom manifest files to the executable.
  ADD_WINDOWS_MANIFEST("${CEF_TARGET}")

  # Copy CEF binary and resource files to the target output directory.
  COPY_FILES("${CEF_TARGET}" "${CEF_BINARY_FILES}" "${CEF_BINARY_DIR}" "${CEF_TARGET_OUT_DIR}")
  COPY_FILES("${CEF_TARGET}" "${CEF_RESOURCE_FILES}" "${CEF_RESOURCE_DIR}" "${CEF_TARGET_OUT_DIR}")
endif()
